


注意: 這裏的練習使用的是 STATA 因爲，我在 R 裏找不到像 STATA 的 `teffects` 這樣靈活且方便的命令，如果你知道，歡迎告訴我: abelardccwang@gmail.com。


數據來自一個觀察性研究，樣本量是 3551 的一個肺癌患者數據庫，四家醫院的肺癌患者正準備選用 1) 常規手術 或者 2) 射頻消蝕 (radiofrequency ablation, RFA) 兩種方法取出肺部的轉移腫塊。該數據的變量如下: 


```{r CI-exe-00,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 
describe
```

其中，主要的混雜因子是: 

1. `hospital`: 有些醫院可能本身更傾向於/不傾向於使用 RFA，或者有些醫院的患者整體症狀較輕/較重;
2. `maxdia`: 如果腫塊太大，那就不適合使用 RFA，而且腫塊較大的患者，生存的概率一般來說比較低;
3. `position`: 腫塊位置，相比較傳統常規手術摘除的方法，RFA 能夠治療那些手術難以摘除的腫塊的部位。

這三個主要的變量被認爲非常重要，需要在分析中被調整。

其他的變量被認爲不太會是混雜因子，但是醫生認爲對患者的預後有很強的預測效果: `age, gender, smoke, nodules, mets, duration, primary`。

最後一點，對於有凝血障礙的患者 `coag` 來說，RFA 是不安全的。


### 在STATA裡打開數據，初步分析和熟悉數據
```{r CI-exe-01,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 

summarize age

tab gender

tab hospital

summarize maxdia

tab position

tab dodp
```


### 用標準邏輯回歸模型分析 `rfa` (暴露) 和 `dodp` (結果) 之間的關係

```{r CI-exe-02,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 

*(a)
logit dodp rfa 

*(b)
logit dodp rfa i.hospital maxdia i.position
```

### 比較上面(a)和(b)兩個邏輯回歸模型的結果，你認為混雜因素對暴露和結果的關係的影響是怎樣的？

總體來說，混雜的模式 (pattern) 應該是正混雜 (positive confounding)。模型 (a)，不經過任何調整，RFA 似乎比標準手術療法好很多 (幾乎減少一半三年內死亡的對數比值 log-odds)。但是調整了其他混雜因素的模型 (b)，結果暗示兩者之間對於患者預後沒有太大的影響 (treatment effect reduced to suggest very little evidence to departure from no effect)。所以調整的這些變量對暴露和結果之間的關係的混雜是正向的(傾向於把關係改變成為接近另假設 tend to change the association to null)。

### 在怎樣的前提假設條件下，上面模型 (b) 可以被賦予因果關係的解釋？

這些假設包括：

1. 無相互幹擾 no interference：某個病人接受的療法，不影響另一個病人療法的結果。
2. 一致性 consistency：對於真的接受了 RFA 療法的病人來說，他/她的療效，和潛在暴露 (potential exposure) 為接受 RFA 療法時的潛在療效 (potential outcome) 是一致的。接受標準手術療法的患者中也是需要一樣的假設。
3. 條件可置換性 conditional exchangeability：對於同一所醫院，腫塊大小相同，腫塊位置相同的患者來說，他/她的兩種潛在治療結果 (potential outcome)，和該病人最終到底是接受了常規手術治療，還是接受 RFA 之間是相互獨立的。用更通俗的話說是，暴露變量 `rfa` 和結果變量 `dodp` 之間的所有可能的混雜，都被模型中加入的 `hospital, maxdia, position` 囊括進去了。
4. 正確的模型結構 correct specification of the model：因為模型 (b) 中不包括任何交互作用的相乘項，要給這個模型擬合的回歸係數以因果關係的解釋，我們需要認為模型中的變量之間沒有任何較互作用，也就是說`rfa`的療效，不因為醫院，腫塊位置，和腫塊大小不同而不同。

### 在前面提出的所有前提假設都滿足的情況下，請給模型 (b) 的回歸係數賦予一個因果效應的解釋。

當四個前提假設都可以滿足，模型 (b) 的`rfa`的回歸係數 `-0.022` 是一個條件對數比值比 (conditional log-odds ratio)。該條件對數比值比調整了醫院，腫瘤大小，和腫瘤位置。患者在三年內死亡或者疾病加重的對數比值 (log odds) 被估計為 0.022 (95% CI: -0.17, 0.21)。這個對數比值比較的是，情形 A.所有患者都被實施 RFA 手術，和情形 B. 所有患者都被實施常規手術摘除腫塊，兩種潛在情形的潛在結果。用潛在結果的數學語言來解釋就是：

$$
\log\{ \frac{\text{Pr}[Y(1) = 1|\mathbf{C = c}]}{1-\text{Pr}[Y(1) = 1|\mathbf{C = c}]} \} - \log\{ \frac{\text{Pr}[Y(0) = 1|\mathbf{C = c}]}{1-\text{Pr}[Y(0) = 1|\mathbf{C = c}]} \}
$$

其中，$\mathbf{C}=$ {`hospital, maxdia, position`}


### 用 STATA 的 `teffects ra` 擬合上面兩個模型

```{r  CI-exe-03,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 

*(a)
teffects ra (dodp, logit) (rfa)

*(b)
teffects ra (dodp i.hospital maxdia i.position, logit) (rfa)
```


### 在怎樣的假設前提條件下，前一步擬合的模型 (b) 結果中的 ATE 可以被賦予因果關係的解釋？


這些假設包括：

1. 無相互幹擾 no interference，解釋同前。
2. 一致性 consistency，解釋同前。
3. 條件可置換性 conditional exchangeability，解釋同前。
4. 正確的模型結構 correct specification of the mode：調整了醫院，腫塊大小，和腫塊位置以後，患者的死亡或者疾病加重的對數比值 (log odds of death or diseae progression) 和腫塊大小，醫院，腫塊位置不再有任何依賴性(independent)，但是接受 RFA 療法和常規手術療法之間的**療效差**，被允許在不同的醫院，腫塊大小，以及腫塊位置的不同而有所不同。

### 前一問和你擬合完簡單的邏輯回歸之後做的模型假設的回答，有什麼不同？

用 STATA 的 `teffects ra` 命令的時候，我們允許了療效差在不同的醫院，不同腫塊的大小，和不同腫塊的位置之間有所不同。The effect of treatment is allowed to differ nby hospital, position of the nodule and its diameter. 這種不同在擬合簡單邏輯回歸模型時是被忽略掉的。

### 用因果關係語言解釋 `teffects ra` 擬合的模型 (b) 的結果

模型 (b) 擬合的結果是邊際因果危險度差 (marginal causal risk difference)。情形 A. 所有患者都被實施 RFA 療法，和情形 B. 所有患者都被實施常規手術療法相比，患者中三年內死亡或者病情加重的比例 (proportion) 被估計要高出 0.026 (95%CI: -0.01, 0.06):

$$
E\{ Y(1) \} - E\{ Y(0) \}
$$

### 如果模型中加入 `age, gender, smoke, nodules, mets, duration, primary` 等和預後相關但是和決定療法並不太有關係的變量，結果會有什麼不同呢？

```{r  CI-exe-04,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 

teffects ra (dodp age gender i.smoke i.hospital nodules mets duration ///
    maxdia i.primary i.position, logit) (rfa)

```

ACE 的估計結果沒有發生非常劇烈的變化，但是，它的標準誤被大大降低了，有效地提高了療效估計的精確度。而且此時的結果已經提示平均因果危險度差是有統計學意義的 (p = 0.006)。這時候，對於整體患者來說，如果全部實施了 RFA，那麼和全部實施標準手術療法相比較會有略差的結果，這個相差是有統計學意義的。

### 如果再向模型中加入和暴露變量相關，和預後沒什麼關係的變量 `coag`，結果該怎麼解讀？

```{r  CI-exe-05,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 
teffects ra (dodp age gender i.smoke i.hospital nodules mets duration ///
    maxdia i.primary i.position coag, logit) (rfa)
```

ACE 的估計量的標準誤因為調整了只和暴露變量相關的變量 `coag` 變得比之前大了一些。但是此時的結果依然提示全部實施 RFA 療法的話結果會比全部實施常規手術治療要差。這裡應該考慮的是，因為 `coag` 本身不是暴露和結果變量之間的混雜因子，我們本不該調整這個變量，一旦調整了只和暴露變量相關的變量，我們**反而會降低療效估計的精確度**，所以不是說模型中想加多少變量就加多少變量的。(Violation of positivity)

**另外，已經有證據證明模型中調整了和暴露相關，但是並不是混雜因子的變量會嚴重增加估計量的偏倚** [@Pearl2011]。


### 使用 `atet` 的選項重新擬合上面的因果效應模型，解釋結果發生的變化，並作出相應的結論。

```{r  CI-exe-06,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 


teffects ra (dodp, logit) (rfa), atet

teffects ra (dodp i.hospital maxdia i.position, logit) (rfa), atet

teffects ra (dodp age gender i.smoke i.hospital nodules mets duration ///
    maxdia i.primary i.position, logit) (rfa), atet



teffects ra (dodp age gender i.smoke i.hospital nodules mets duration ///
    maxdia i.primary i.position coag, logit) (rfa), atet
```

模型的最後加了 `atet` 之後最大的不同是導致結果分析的**結論完全改變了**。這時候 RFA 的療效反而變得有利起來。每個模型都給出了小於零的**治療組因果危險度差 (causal risk difference in the treated)**。這主要是因為接受 RFA療法和常規手術治療的兩類患者本身有太大的不同 (strong effect heterogeneity)。因為患者如果腫塊尺寸小， RFA 可以給出很好的預後，如果腫塊的位置太難找無法使用標準療法的時候又只能選用RFA。這些都是決定醫生最終給患者使用哪種療法的重要參考因素。所以如果 ATT 是小於零的，意味著，在那些(被)選擇了 RFA療法的患者中，療效是良好的，可以有效的減少三年內死亡和病情加重的概率。

另外值得一提的是，這裡調整了 `coag` 對ATT 估計的影響較小。這主要是因為，如果一個患者有凝血功能障礙，他/她就不可能接受 RFA 療法: 

```{r  CI-exe-07,  engine='stata', cache=TRUE, echo=FALSE}
use "../backupfiles/RFA.dta" 
tab coag rfa, col chi
```

但是一個患者如果沒有凝血功能障礙，那麼他/她接受哪種療法就變得不受 `coag` 的影響。而此時我們使用 `etat` 估算的是所有接受 RFA 療法的患者中的療效。這裡受到模型變量共線性影響就較小。
